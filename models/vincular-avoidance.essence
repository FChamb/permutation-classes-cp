language Essence 1.3

given length : int
given avoiding : set of (sequence (injective) of int, sequence of int)

find perm : sequence (size length, injective) of int(1..length)


$ An occurrence of P in T corresponds to a subsequence T(i_1)T(i_2)...T(i_k) of T that is order-isomorphic to P and for which T (i_(j+1)) = T (i_j + 1) whenever P contains P(j)P(j+1).
such that
    forAll (av,cons) in avoiding .
        !(exists ix : matrix indexed by [int(1..|av|)] of int(1..length) .
            (forAll i,j : int(1..|av|) . i < j -> ix[i] < ix[j]) /\
            (forAll n1, n2 : int(1..|av|) , n1 < n2 .
                (av(n1) < av(n2) <-> perm(ix[n1]) < perm(ix[n2])) /\
                    (forAll c in cons , c = n1 /\ c+1 = n2 . 
                        (perm(ix[c+1]) = perm(ix[c]+1)))
            )
        )